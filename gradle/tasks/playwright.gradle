apply plugin: 'com.github.node-gradle.node'

node {
    download = true
}

task playwrightNpxInstall(type: NpxTask, dependsOn: npmInstall) {
    group 'Testing'
    description 'Installs Playwright dependencies. Playwright/test is already installed via npmInstall'

    environment = [
            'PLAYWRIGHT_BROWSERS_PATH': '0'
    ]
    command = 'playwright'
    args = ['install', 'chromium']
}

task playwrightLint(type: NpmTask, dependsOn: npmInstall) {
    group 'Testing'
    description 'Lints the tests/ directory across a number of tools. Can also be run as an npm script (see package.json)'

    args = ['run', 'lint']
}

task playwrightFormat(type: NpmTask, dependsOn: npmInstall) {
    group 'Testing'
    description 'Formats the files in the tests/ directory via prettier. Can also be run as an npm script (see package.json)'

    args = ['run', 'format']
}

task playwrightRun(type: NpxTask, dependsOn: [project.tasks.playwrightNpxInstall, project.tasks.playwrightLint]) {
    group 'Testing'
    description 'Runs Playwright scripts as per tests/uportal-pw.config.ts'
    environment = [
            'PLAYWRIGHT_BROWSERS_PATH': '0'
    ]
    command = 'playwright'
    args = ['test', '--config=tests/uportal-pw.config.ts']
}

// Works best when only a single test is enabled ( test.only(...) )
task playwrightDebug(type: NpxTask, dependsOn: [project.tasks.playwrightNpxInstall, project.tasks.playwrightLint]) {
    group 'Testing'
    description 'Runs Playwright scripts as per tests/uportal-pw.config.ts in debug mode'
    environment = [
            'PLAYWRIGHT_BROWSERS_PATH': '0',
            'PWDEBUG': 'console'
    ]
    command = 'playwright'
    args = ['test', '--config=tests/uportal-pw.config.ts', '--debug']
}
