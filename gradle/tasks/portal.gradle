task portalInit {
    group 'Portal'
    description 'All-in-one task that starts the integrated HSQLDB instance, downloads & installs ' +
            'the integrated Tomcat servlet container, deploys all uPortal technology to Tomcat, ' +
            'creates the database schema, and imports both the Base & Implementation data sets'
    dependsOn allprojects.collect { it.tasks.matching { it.name.equals('clean') } }
    dependsOn ':hsqlStart'
    dependsOn ':tomcatInstall'
    dependsOn allprojects.collect { it.tasks.matching { it.name.equals('tomcatDeploy') } }
    dependsOn allprojects.collect { it.tasks.matching { it.name.equals('dataInit') } }

    // when running the uPortal tomcatDeploy, you may also deploy session replication code
    // if deploySessionReplicationAop is true in gradle.properties
    def isDeploySessionReplicationEnabled = project.rootProject.properties.deploySessionReplicationAop.toBoolean()
    if (isDeploySessionReplicationEnabled) {
        dependsOn ":deployAopArchives"
    }
}

task portalOpen() {
    group 'Portal'
    description 'Opens the Default URL in a browser'
    dependsOn project.tasks.portalProperties
    dependsOn project.tasks.tomcatStart

    doLast {
        String portalUrl = 'http://localhost:8080/uPortal/' // Can we calculate?
        logger.lifecycle("Opening uPortal in the default browser at ${portalUrl}")
        java.awt.Desktop.desktop.browse portalUrl.toURI()
    }
}
