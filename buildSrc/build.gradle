/*
 * The buildSrc is a separate Gradle project and does not source
 * gradle.properties;  but we can make that happen, since we have
 * some properties in common.
 */
String gradlePropsLocation = new File('gradle.properties').exists() ? 'gradle.properties' : '../gradle.properties'
Properties gradleProperties = new Properties()
gradleProperties.load(new FileInputStream(gradlePropsLocation))
ext {
    gradleProperties.each { name, value ->
        set(name, value)
    }

}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile ("org.apache.portals.pluto:pluto-util:${plutoVersion}") {
        exclude group: 'commons-io', module: 'commons-io'
    }
    compile "commons-io:commons-io:${commonsIoVersion}"

    compileOnly "org.jasig.portal:uPortal-tools:${uPortalVersion}"
}
